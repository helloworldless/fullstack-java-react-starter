plugins {
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
}

allprojects {
    repositories {
        jcenter()
    }
}

def javaProjects = subprojects.findAll {
    it.name != "ui"
}

configure(javaProjects) {
    group = 'com.davidagood'
    version = '1.0.0-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'

    repositories {
        jcenter()
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:2.1.9.RELEASE")
        }
    }

    compileJava {
        sourceCompatibility = 12
        targetCompatibility = 12
    }

    checkstyle {
        toolVersion "7.6.1"
        configFile = rootProject.file('checkstyle.xml')
    }

    tasks.withType(Checkstyle) {
        reports {
            xml.enabled false
            html.enabled true
        }
    }

}
